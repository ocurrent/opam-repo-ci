(* SPDX-License-Identifier: Apache-2.0
 * Copyright (c) 2024 Puneeth Chaganti <punchagan@muse-amuse.in>, Shon Feder <shon.feder@gmail.com>, Tarides <contact@tarides.com>
 *)

(** Some package name prefixes must be used along with specific conflict classes

    If either a restricted prefix or conflict class exists, then the
    corresponding other must also exist. *)
type prefix_conflict_class_mismatch =
  | WrongPrefix of { conflict_class : string; required_prefix : string }
  | WrongConflictClass of { prefix : string; required_conflict_class : string }

(** Errors detected during linting.

    Use {!string_of_error} to produce descriptions of the errors *)
type error =
  | UnnecessaryField of string
  | UnmatchedName of (OpamPackage.Name.t * OpamPackage.Name.t)
  | UnmatchedVersion of (OpamPackage.Version.t * OpamPackage.Version.t)
  | DubiousDuneSubst
  | DuneIsBuild
  | NoPackageSources
  | UnexpectedFile of string
  | ForbiddenPerm of string
  | OpamLint of (int * [ `Warning | `Error ] * string)
  | MaintainerWithoutContact of string list
  | NameCollision of string
  | WeakChecksum of string
  | PinDepends
  | ExtraFiles
  | RestrictedPrefix of string
  | PrefixConflictClassMismatch of prefix_conflict_class_mismatch
  | DefaultTagsPresent of string list
  | MissingUpperBound of string
  | InvalidReasonForArchiving
  | InvalidOpamRepositoryCommitHash
  | InvalidConfPackage of [ `Conf_prefix | `Depext | `Conf_flag ] list

(**/**)

let is_warning = function
  | UnnecessaryField _ | DubiousDuneSubst | DuneIsBuild | NameCollision _
  | DefaultTagsPresent _ | RestrictedPrefix _ ->
      true
  | _ -> false

(* These x_ fields are used in the opam repo archive *)
let x_reason_for_archiving_field = "x-reason-for-archiving"

let x_reason_for_archiving_valid_reasons =
  [
    "ocaml-version"; "source-unavailable"; "maintenance-intent"; "uninstallable";
  ]

let x_opam_repository_commit_hash_at_time_of_archiving_field =
  "x-opam-repository-commit-hash-at-time-of-archiving"

let msg_of_prefix_conflict_class_mismatch = function
  | WrongPrefix { conflict_class; required_prefix } ->
      Printf.sprintf
        "package with conflict class '%s' requires name prefix '%s'"
        conflict_class required_prefix
  | WrongConflictClass { prefix; required_conflict_class } ->
      Printf.sprintf "package with prefix '%s' requires conflict class '%s'"
        prefix required_conflict_class

(**/**)

(** [string_of_error] returns a string description of an error*)
let string_of_error = function
  | UnnecessaryField field ->
      Printf.sprintf "Unnecessary field '%s'. It is suggested to remove it."
        field
  | UnmatchedName (value, expected) ->
      Printf.sprintf
        "The field 'name' that doesn't match its context. Field 'name' has \
         value '%s' but was expected of value '%s'."
        (OpamPackage.Name.to_string value)
        (OpamPackage.Name.to_string expected)
  | UnmatchedVersion (value, expected) ->
      Printf.sprintf
        "The field 'version' that doesn't match its context. Field 'version' \
         has value '%s' but was expected of value '%s'."
        (OpamPackage.Version.to_string value)
        (OpamPackage.Version.to_string expected)
  | DubiousDuneSubst ->
      Printf.sprintf
        "Dubious use of 'dune subst'. 'dune subst' should always only be \
         called with {dev} (i.e. [\"dune\" \"subst\"] {dev}) If your opam file \
         has been autogenerated by dune, you need to upgrade your dune-project \
         to at least (lang dune 2.7)."
  | WeakChecksum msg ->
      Printf.sprintf
        "Weak checksum algorithm(s) provided. Please use SHA-256 or SHA-512. \
         Details: %s"
        msg
  | PinDepends ->
      Printf.sprintf
        "pin-depends present. This is not allowed in the opam-repository."
  | ExtraFiles ->
      Printf.sprintf
        "extra-files present. This is not allowed in the opam-repository. \
         Please use extra-source instead."
  | NoPackageSources -> Printf.sprintf "No package source directory provided."
  | DuneIsBuild ->
      Printf.sprintf
        "The package tagged dune as a build dependency. Due to a bug in dune \
         (https://github.com/ocaml/dune/issues/2147) this should never be the \
         case. Please remove the {build} tag from its filter."
  | RestrictedPrefix prefix ->
      Printf.sprintf "package name has restricted prefix '%s'" prefix
  | PrefixConflictClassMismatch mismatch ->
      msg_of_prefix_conflict_class_mismatch mismatch
  | NameCollision other_ ->
      Printf.sprintf "Possible name collision with package '%s'" other_
  | UnexpectedFile file -> Printf.sprintf "Unexpected file in %s" file
  | ForbiddenPerm file ->
      Printf.sprintf
        "Forbidden permission for file %s. All files should have permissions \
         644."
        file
  | OpamLint warn ->
      let warn = OpamFileTools.warns_to_string [ warn ] in
      String.trim warn |> Printf.sprintf "Opam lint %s"
  | MaintainerWithoutContact maintainer ->
      Printf.sprintf
        "There is no way to contact the maintainer(s) '%s'. A package must \
         either specify a url for 'bug-reports' or provide an email address in \
         the 'maintainer' field."
        (String.concat ", " maintainer)
  | DefaultTagsPresent tags ->
      Printf.sprintf
        "The package has not replaced the following default, example tags: %s"
        (String.concat ", " tags)
  | MissingUpperBound dep_name ->
      Printf.sprintf "An upper bound constraint is missing on dependency '%s'"
        dep_name
  | InvalidReasonForArchiving ->
      Printf.sprintf
        "The field '%s' must be present and hold a nonempty list of one or \
         more of the valid reasons %s"
        x_reason_for_archiving_field
        (String.concat ", " x_reason_for_archiving_valid_reasons)
  | InvalidOpamRepositoryCommitHash ->
      Printf.sprintf
        "The field '%s' must be present and hold a string recording the commit \
         hash of the primary repo at the time the package version is archived."
        x_opam_repository_commit_hash_at_time_of_archiving_field
  | InvalidConfPackage properties ->
      let property_descriptions =
        properties
        |> List.map (function
             | `Conf_flag -> "the 'conf' flag"
             | `Conf_prefix -> "the 'conf-' name prefix"
             | `Depext -> "a non-empty 'depext' field")
        |> String.concat " and "
      in
      Printf.sprintf
        "conf packages should always use the 'conf-' name prefix, the 'conf' \
         flag, and the 'depext' field all together, but this package only has \
         %s"
        property_descriptions

(** [msg_of_error (pkg, err)] is a string describing a linting [err] found for the [pkg] *)
let msg_of_error (package, (err : error)) =
  let pkg = OpamPackage.to_string package in
  let prefix = if is_warning err then "Warning" else "Error" in
  string_of_error err |> Printf.sprintf "%s in %s: %s" prefix pkg
